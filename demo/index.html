<html lang="en">
   <head>
      <meta charset="utf-8">

      <link rel="icon" type="image/png" href="http://allotrop3.github.io/four/favicon.png">

      <title>Four</title>

      <link rel="stylesheet" type="text/css" href="http://allotrop3.github.io/four/styles/four.css">
   </head>

   <body>
      <canvas></canvas>

      <script class="four__shader--default" type="x-shader/x-vertex">
         #version 100

         precision lowp float;

         struct camera
         {
            mat4 projectionMatrix;
            mat4 modelViewMatrix;
            mat3 normalMatrix;
         };

         attribute vec3 a_position;
         attribute vec2 a_uv;
         attribute vec3 a_normal;

         uniform camera u_camera;

         varying vec3 v_position;
         varying vec2 v_uv;
         varying vec3 v_normal;

         void main(void)
         {
            vec4 position = vec4(a_position, 1.0);

            gl_Position = u_camera.projectionMatrix * u_camera.modelViewMatrix * position;

            v_position = vec3(u_camera.modelViewMatrix * position);
            v_uv = a_uv;
            v_normal = normalize(u_camera.normalMatrix * a_normal);
         }
      </script>

      <script class="four__shader--default" type="x-shader/x-fragment">
         #version 100

         precision lowp float;

         struct material
         {
            sampler2D image;
            vec3 ambient;
            vec3 diffuse;
            vec3 specular;
            int shading;
            float shininess;
         };

         struct light
         {
            vec3 ambient;
            vec3 diffuse;
            vec3 specular;
            vec3 attenuation;
            float intensity;
            vec3 location;
         };

         uniform material u_material;
         uniform light u_light;

         varying vec3 v_position;
         varying vec2 v_uv;
         varying vec3 v_normal;

         vec3 phong(vec3 base)
         {
            vec3 normal = normalize(v_normal);
            vec3 direction = u_light.location - v_position;
            vec3 s = normalize(direction);
            vec3 v = normalize(-v_position);
            vec3 reflection = reflect(-s, normal);
            vec3 ambient = u_light.ambient * u_material.ambient;
            float sdotn = max(dot(s, normal), 0.0);
            vec3 diffuse = u_light.intensity * u_light.diffuse * (u_material.diffuse + base) * sdotn;
            vec3 specular = vec3(0.0);
            float range = length(direction);
            float attenuation = 1.0 / (u_light.attenuation.x + u_light.attenuation.y * range + u_light.attenuation.z * pow(range, 2.0));

            if (sdotn > 0.0)
            {
               specular = u_light.specular * u_material.specular;
               specular = specular * pow(max(dot(reflection, v), 0.0), u_material.shininess);
            }

            return (ambient + diffuse + specular) * attenuation;
         }

         void main(void)
         {
            vec3 base = texture2D(u_material.image, v_uv).xyz;
            vec3 color = phong(base);

            gl_FragColor = vec4(color, 1.0);
         }
      </script>

      <script src="http://allotrop3.github.io/four/scripts/four.min.js"></script>
      <script src="http://allotrop3.github.io/four/scripts/test.js"></script>
   </body>
</html>